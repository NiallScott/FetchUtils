/*
 * Copyright (C) 2014 - 2015 Niall Scott
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 4
        targetSdkVersion 22
        versionCode Integer.parseInt(VERSION_CODE)
        versionName VERSION_NAME
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    jacoco {
        version = "0.7.1.201405082137"
    }

    android.libraryVariants.all { variant ->
        /*
         * This task generates JavaDoc.
         */
        task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
            description "Generates Javadoc for ${variant.name}"
            title = "FetchUtils for Android Javadoc"
            source = variant.javaCompile.source
            classpath = files(variant.javaCompile.classpath.files, android.bootClasspath)
            options {
                linksOffline("http://d.android.com/reference",
                        "${android.sdkDirectory}/docs/reference")
            }
        }

        /**
         * This task packages Javadoc in to a JAR file, required for pushing to Maven.
         */
        task("package${variant.name.capitalize()}Javadoc", type: Jar,
                dependsOn: getTasks().getByName("generate${variant.name.capitalize()}Javadoc")) {
            description "Package Javadoc for ${variant.name} as a JAR"
            classifier = 'javadoc'
            from getTasks().getByName("generate${variant.name.capitalize()}Javadoc").destinationDir
        }

        /**
         * This task packages the sources in to a JAR file, required for pushing to Maven.
         */
        task("package${variant.name.capitalize()}Sources", type: Jar) {
            description "Package sources for ${variant.name} as a JAR"
            classifier = "sources"
            from variant.javaCompile.source
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-annotations:22.0.0'
    compile 'com.android.support:support-v4:22.0.0'
}

apply from: '../gradle-mvn-push.gradle'